find_path(PCRE2_INCLUDE_DIR
    NAMES pcre2.h
    PATHS ${CONAN_INCLUDE_DIRS_PCRE2}
    NO_DEFAULT_PATH
)

find_library(PCRE2_LIBRARY
    NAMES pcre2-8 pcre2-8d pcre2-8-static pcre2-8-staticd pcre2-16 pcre2-16d pcre2-16-static pcre2-16-staticd pcre2-32 pcre2-32d pcre2-32-static pcre2-32-staticd
    PATHS ${CONAN_LIB_DIRS_PCRE2}
    NO_DEFAULT_PATH
)

find_library(PCRE2POSIX_LIBRARY
    NAMES pcre2-posix pcre2-posixd pcre2-posix-static pcre2-posix-staticd
    PATHS ${CONAN_LIB_DIRS_PCRE2}
    NO_DEFAULT_PATH
)

if(PCRE2_INCLUDE_DIR)

    file(STRINGS ${PCRE2_INCLUDE_DIR}/pcre2.h DEFINE_PCRE2_MAJOR REGEX "^#define PCRE2_MAJOR")
    string(REGEX REPLACE "^.*PCRE2_MAJOR +([0-9]+).*$" "\\1" PCRE2_VERSION_MAJOR "${DEFINE_PCRE2_MAJOR}")

    file(STRINGS ${PCRE2_INCLUDE_DIR}/pcre2.h DEFINE_PCRE2_MINOR REGEX "^#define PCRE2_MINOR")
    string(REGEX REPLACE "^.*PCRE2_MINOR +([0-9]+).*$" "\\1" PCRE2_VERSION_MINOR "${DEFINE_PCRE2_MINOR}")

    set(PCRE2_VERSION_STRING "${PCRE2_VERSION_MAJOR}.${PCRE2_VERSION_MINOR}")
    set(PCRE2_VERSION ${PCRE2_VERSION_STRING})
    set(PCRE2_VERSION_COUNT 2)

endif()

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(PCRE2
    REQUIRED_VARS PCRE2_INCLUDE_DIR PCRE2_LIBRARY
    VERSION_VAR PCRE2_VERSION
)

if(PCRE2_FOUND AND NOT TARGET PCRE2::PCRE2)

    add_library(PCRE2::PCRE2 UNKNOWN IMPORTED)
    set_target_properties(PCRE2::PCRE2 PROPERTIES
        IMPORTED_LOCATION ${PCRE2_LIBRARY}
        INTERFACE_INCLUDE_DIRECTORIES ${PCRE2_INCLUDE_DIR}
        INTERFACE_COMPILE_DEFINITIONS "${CONAN_COMPILE_DEFINITIONS_PCRE2}"
    )

    mark_as_advanced(PCRE2_INCLUDE_DIR PCRE2_LIBRARY)

    set(PCRE2_INCLUDE_DIRS ${PCRE2_INCLUDE_DIR})
    set(PCRE2_LIBRARIES ${PCRE2_LIBRARY} ${PCRE2POSIX_LIBRARY})
    set(PCRE2_DEFINITIONS "${CONAN_COMPILE_DEFINITIONS_PCRE2}")

endif()

